plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("org.jetbrains.kotlin.plugin.compose")
}

android {
    namespace = "com.safal207.androidreliabilitylab"
    compileSdk = 35

    defaultConfig {
        applicationId = "com.safal207.androidreliabilitylab"
        minSdk = 26
        targetSdk = 35
        versionCode = 1
        versionName = "0.1.0"

        val incidentBaseUrl = providers.gradleProperty("incidentBaseUrl")
            .getOrElse("http://10.0.2.2:8765/")
        require(incidentBaseUrl.matches(Regex("https?://[A-Za-z0-9.:-]+/"))) {
            "incidentBaseUrl must be an HTTP(S) origin ending in /"
        }
        buildConfigField("String", "INCIDENT_BASE_URL", "\"$incidentBaseUrl\"")
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    sourceSets.getByName("test").resources.srcDir(rootProject.file("fixtures"))

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }
}

dependencies {
    val composeBom = platform("androidx.compose:compose-bom:2024.12.01")
    implementation(composeBom)

    implementation("androidx.activity:activity-compose:1.10.0")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.8.7")
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7")
    implementation("com.squareup.retrofit2:retrofit:2.11.0")
    implementation("com.squareup.retrofit2:converter-gson:2.11.0")
    implementation("com.squareup.okhttp3:okhttp:4.12.0")

    debugImplementation("androidx.compose.ui:ui-tooling")

    testImplementation("junit:junit:4.13.2")
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.9.0")
    testImplementation("com.squareup.okhttp3:mockwebserver:4.12.0")
}
